plugins {
    id 'application'
    id 'idea'
}

description = "Thrift Examples"

repositories {
    mavenCentral()
    mavenLocal()
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.+'

dependencies {
    implementation "grpc-ecosystem.grift:grift:1.0.0-SNAPSHOT"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
}

// Add pre-generated code to build path
String generatedSourcePath = "${projectDir}/src/generated"
project.sourceSets {
    main {
        java {
            srcDir "${generatedSourcePath}/main/java"
            srcDir "${generatedSourcePath}/main/grpc"
        }
    }
}

// Inform IntelliJ projects about the generated code.
idea {
    module {
        sourceDirs += file("${generatedSourcePath}/main/java")
        sourceDirs += file("${generatedSourcePath}/main/grpc")
    }
}

// Provide convenience executables for trying out the examples.
startScripts.enabled = false
task thriftHelloWorldServer(type: CreateStartScripts) {
    mainClassName = "io.grpc.examples.thrift.helloworld.HelloWorldServer"
    applicationName = "thrift-hello-world-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task thriftHelloWorldClient(type: CreateStartScripts) {
   mainClassName = "io.grpc.examples.thrift.helloworld.HelloWorldClient"
   applicationName = "thrift-hello-world-client"
   outputDir = new File(project.buildDir, 'tmp')
   classpath = startScripts.classpath
}

applicationDistribution.into("bin") {
    from(thriftHelloWorldServer)
    from(thriftHelloWorldClient)
    fileMode = 0755
}
